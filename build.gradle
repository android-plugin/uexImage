apply plugin: 'com.android.application'

dependencies {
    compile fileTree(include: '*.jar', dir: 'libs')
}

repositories {
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.1"
    lintOptions {
        abortOnError false
    }


    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
            jniLibs.srcDirs=['libs']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

def pluginName="uexImage"
def buildConfigPath="org/zywx/wbpalmstar/widgetone/"

def jarDir=pluginName+'/jar/'
def jarName="plugin_"+pluginName+".jar"
def jarPath=jarDir+jarName

task clearJar(type: Delete) {
    delete jarPath;
}

task makeJar(type:Jar,dependsOn:['compileReleaseJavaWithJavac']) {
    archiveName jarName
    from('build/intermediates/classes/release/')
    destinationDir=file(jarDir)
    exclude('**/R.class')
    exclude('**/R\$*.class')
    exclude('**/BuildConfig.class')
    exclude(buildConfigPath)
}

makeJar.dependsOn(clearJar)
